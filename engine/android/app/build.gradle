apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33

        //////////// MODIFY FOR CUSTOM APK /////////////////
        applicationId "org.openbor.engine"
        versionCode 1
        versionName "2.0.0"
        resValue "string", "app_name", "OpenBOR"
        resValue "string", "app_desc", "Ultimate 2D Game Engine"
        ///////////////////////////////////////////////////

        externalNativeBuild {
            ndkBuild {
                arguments "APP_PLATFORM=android-23"
                abiFilters 'armeabi-v7a', 'arm64-v8a' // 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    signingConfigs {
        release {
            if (project.gradle.startParameter.taskNames.any { it.toLowerCase().contains('release') }) {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            // Optional: Custom configurations for debug builds
        }
    }

    lint {
        abortOnError false
    }

    namespace 'org.openbor.engine'

    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            ndkBuild {
                path 'jni/Android.mk'
            }
        }
    }

    // Custom output filename
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "debug") {
                output.outputFileName = "OpenBOR.apk"
            } else if (variant.buildType.name == "release") {
                output.outputFileName = "OpenBOR-release.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.car.app:app:1.2.0-rc01"
}
